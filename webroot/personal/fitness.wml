# $Id$
#include <template.wml> topic="about"

# Some Perl functions to support the statistics in this page:
<:
use strict;
use warnings;
use Storable;
use GPS::GarminConnect;
use Config::Simple;
use POSIX;
use Data::Dumper;

# SUBROUTINE:  activities()
# DESCRIPTION: Returns a reference to a list of all activities. If cached data
#              is available and recent enough, that is what is returned.
#              Otherwise, fetches the data from Garmin Connect.
sub activities {
  my $configfile = $ENV{HOME} . '/.websiterc';
  my $cachefile = '/tmp/activities.cache';

  # Is the cache available and recent?
  my $recent_enough = mktime(localtime(time - 60*60*23)); # 23 hours ago
  if( -r $cachefile && (stat(_))[9] >= $recent_enough ) {
    return retrieve($cachefile);
  }

  # Call Garmin Connect to get the data.
  # Get the user's Garmin Connect username/password.
  my $config = new Config::Simple($configfile);
  my $connect = GPS::GarminConnect->new(
    username => $config->param('garmin_connect.username'),
    password => $config->param('garmin_connect.password')
  );
  my @activities = $connect->activities();

  # Cache the activities before returning them.
  if( @activities ) {
    store(\@activities, $cachefile);
  }

  return \@activities;
}

# SUBROUTINE:  milesRunInYear($year)
# DESCRIPTION: Returns the number of miles run in $year.
sub milesRunInYear {
  my ($year) = @_;
  my $activities = activities();
  my $total_miles = 0;
  foreach my $a ( @{$activities} ) {
    # Count only running activities
    next unless $a->{activity}->{activityType}->{type}->{display} eq 'Running';

    # Count only activities from the requested year
    my $activity_year = 1900 +
     (localtime($a->{activity}->{beginTimestamp}->{millis}/1000))[5];
    next unless $activity_year == $year;

    $total_miles += $a->{activity}->{sumDistance}->{value};
  }

  # Return a number with a reasonable precision
  return sprintf("%0.2f", $total_miles);
}

# SUBROUTINE:  fastestRuns( minMiles => $min,
#                           maxMiles => $max,
#                           numResults => $num )
# DESCRIPTION: Returns the $num fastest runs that are at least $min miles
#              but not more than $max miles in length. You can specify
#              just a minimum bound or just a maximum, or both. The results
#              are returned as a list reference of activities.
sub fastestRuns {
  my %opts = @_;
  if( !defined $opts{numResults} ) {
    die "numResults is required";
  }
  if( !defined $opts{minMiles} && !defined $opts{maxMiles} ) {
    die "at least one of minMiles or maxMiles is required";
  }

  my $activities = activities();
  # Create a list of just runs which match the min/max, which we will sort
  # later.
  my @matchingRuns;
  foreach my $a ( @{$activities} ) {
    # Count only running activities
    next unless $a->{activity}->{activityType}->{type}->{display} eq 'Running';

    # Enforce the minimum distance
    if( defined $opts{minMiles} ) {
      next unless $a->{activity}->{sumDistance}->{value} >= $opts{minMiles};
    }

    # Enforce the maximum distance
    if( defined $opts{maxMiles} ) {
      next unless $a->{activity}->{sumDistance}->{value} <= $opts{maxMiles};
    }

    push @matchingRuns, $a;
  }

  # Sort the matching run list so that the fastest runs are first.
  @matchingRuns = sort {
    $a->{activity}->{weightedMeanSpeed}->{value} <=>
      $b->{activity}->{weightedMeanSpeed}->{value}
  } @matchingRuns;

  # Slice off however many the user requested from the front of the list.
  my @fastestRuns = @matchingRuns[0 .. $opts{numResults}-1];

  return \@fastestRuns;
}
:>

<title "Fitness">

<heading>Fitness</heading>

<h3>Background</h3>

<p>I like to run for exercise, and for years I have been using a Garmin
ForeRunner to keep track of my pace and distance. This is much more accurate
than using a pedometer, which is just counting your steps and taking a guess
as to how far you've run. Also, I can upload my data to Garmin Connect so
that I can review the history and get interesting statistics like the
ones below. Well, they're interesting to me, at least.</p>

<h3>Miles run per year</h3>

<p>It wasn't until 2010 that I started entering
all my treadmill runs manually. There are a few months each summer when it's
too hot for a long run outside. So the years prior to 2010 are counting
only outdoor runs.</p>

<ul>
<: foreach my $year ( 2008 .. ((localtime(time))[5]+1900) ) { _:>
  <li><:= $year :>: <:= milesRunInYear($year) :> miles</li>
<: } :>
</ul>

<h3>Fastest long runs</h3>

<p>I run a long run once a week, usually on the weekend. Lately these have
been about 12-13 miles. This is a list of the fastest runs over 10 miles.</p>

<table class="chart">
<tr>
  <th>Title</th>
  <th>Date</th>
  <th>Speed</th>
  <th>Distance</th>
</tr>
<: foreach my $run ( @{fastestRuns(minMiles => 10, numResults => 5)} ) { _:>
<tr>
  <td><a href="http://connect.garmin.com/activity/<:= $run->{activity}->{activityId} :>"><:= $run->{activity}->{activityName}->{value} :></a></td>
  <td><:= $run->{activity}->{beginTimestamp}->{abbr} :></td>
  <td><:= $run->{activity}->{weightedMeanSpeed}->{withUnitAbbr} :></td>
  <td><:= $run->{activity}->{sumDistance}->{withUnitAbbr} :></td>
</tr>
<: } :>
</table>

<h3>Fastest short runs</h3>

<p>Not to be left out, here are the fastest short runs. These are runs three
to six miles in length.</p>

<table class="chart">
<tr>
  <th>Title</th>
  <th>Date</th>
  <th>Speed</th>
  <th>Distance</th>
</tr>
<: foreach my $run ( @{fastestRuns(minMiles => 3, maxMiles => 6, numResults => 5)} ) { _:>
<tr>
  <td><a href="http://connect.garmin.com/activity/<:= $run->{activity}->{activityId} :>"><:= $run->{activity}->{activityName}->{value} :></a></td>
  <td><:= $run->{activity}->{beginTimestamp}->{abbr} :></td>
  <td><:= $run->{activity}->{weightedMeanSpeed}->{withUnitAbbr} :></td>
  <td><:= $run->{activity}->{sumDistance}->{withUnitAbbr} :></td>
</tr>
<: } :>
</table>

